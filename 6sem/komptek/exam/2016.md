# Exam 2016

## Problem 1-5 is done on paper

## Problem 6

```lex
%{
  #include "y.tab.h"
%}
%option noyywrap
%%
[0-9]+ { return INTEGER; }
. { return yytext[0]; }
%%
```

```c
%{
  #include <stdio.h>
  #include <stdlib.h>
  extern char *yytext;
%}
%token INTEGER
%left '+' '-'
%left '*' '/'
%nonassoc UMINUS
%%
program:
    expression '.' {
      // we are finished, call printf
      // first fetch the out string to rdi
      printf("movq $OUT, %%rdi\n");
      // then fetch from top of stack
      printf("popq %%rsi\n");
      // and call printf
      printf("call printf\n");
    }
;
expression:
    expression '+' expression {
      // handle plus
      // pop topmost from stack, and store in the stack
      printf("popq %%rax\n");
      printf("addq %%rax, (%%rsp)\n");
    }
  | expression '-' expression {
      // handle minus
      // pop topmost from stack, and store in the stack
      printf("popq %%rax\n");
      printf("subq %%rax, (%%rsp)\n");
    }
  | expression '*' expression {
      // handle multiplication
      // pop from top of stack to rax
      printf("popq %%rax\n");
      // sign extend
      printf("cqo\n");
      // multiply
      printf("imulq (%%rsp)\n");
      //replace value in stack
      printf("movq %%rax, (%%rsp)\n");
    }
  | expression '/' expression {
      // handle divisoin
      // pop from top of stack to r10
      printf("popq %%r10\n");
      // pop from stack to rax (quotient)
      printf("popq %%rax\n");
      // sign extend
      printf("cqo\n");
      // divde rax:rdi by r10
      printf("idivq %%r10\n");
      // push to stack
      printf("pushq %%rax\n");
    }
  | '-' expression %prec UMINUS {
      // negate
      printf("negq (%%rsp)\n");
    }
  | '(' expression ')' {}
  | INTEGER { printf ( "pushq $%ld\n", strtol ( yytext, NULL, 10 ) ); }
;
%%
int yyerror( const char *error ) {
fprintf ( stderr, "%s\n", error );
exit ( EXIT_FAILURE );
}
int main () {
  puts ( ".section .data" );
  puts ( "OUT: .string \"%d\\n\"" );
  puts ( ".globl main" );
  puts ( ".section .text" );
  puts ( "main:" );
  puts ( "pushq %rbp" );
  puts ( "movq %rsp,%rbp" );
  yyparse();
  puts ( "leave" );
  puts ( "ret" );
  exit ( EXIT_SUCCESS );
}
```
